using Nemerle.Collections;
using Nemerle.Utility;
using System.Collections.Generic;
using System.Console;
using System.IO;
using System;

public module IpcGenerator
{
	public GenerateIpcFromThrift
		( thriftFilePath  : string
		, targetDirectory : string
		, targetLanguage  : TargetLanguage
		) : void
	{
		{
			match (Parser().Parse(File.ReadAllText(thriftFilePath)))
			{
			| Some(x) =>
				def srcName = Path.GetFileName(thriftFilePath);
				def api = MapDocument(x, targetLanguage, srcName);

				Write(api.ToString());

				match (targetLanguage)
				{
				| TargetLanguage.Cpp =>
					def generator = CPP.Generator("  ");
					generator.WriteCode(api, targetDirectory);
				}
			| None =>
				WriteLine("parsing failure")
			}
		}
	}

	private GetLanguageName(_ : TargetLanguage) : string
	{
	| TargetLanguage.Cpp => "cpp"
	}

	// transform the AST to simplify code gen
	private MapDocument
		( document       : AST.Document
		, targetLanguage : TargetLanguage
		, srcName        : string
		) : AST2.Api
	{
		//--------------
		// get namespace
		//--------------

		def GetVerifiedNamespaces(headers)
		{
			def namespaces = Hashtable();
			foreach (header in headers)
			{
				match (header)
				{
				| AST.Header.Namespace(id, lang) =>
					if (namespaces.Contains(lang))
						throw GeneratorException($"Multiple namespace statements for '$lang' in '$srcName'.");
					else
						namespaces[lang] = id.SplitToList(array['.']);
				| _ => ()
				}
			}
			namespaces;
		}
		def ns = AST2.Namespace
			( parts = GetVerifiedNamespaces(document.Headers).GetValueOrDefault
				( key          = GetLanguageName(targetLanguage)
				, defaultValue = []
				)
			);
		def globalNS = AST2.Namespace([]);

		//----------------
		// map definitions
		//----------------

		def unresolvedTypes = List();

		def MapConstant(c)
		{
		| AST.Constant.Int(x) => AST2.Constant.Int(x)
		}
		and MapDefinition(d : AST.Definition)
		{
		| Const(ty, value)   => AST2.Definition.Const     (d.name, ns, MapType(ty), MapConstant(value))
		| Enum(members)      => AST2.Definition.Enum      (d.name, ns, members.Map(MapEnumMember))
		| Exception(fields)  => AST2.Definition.Exception (d.name, ns, fields.Map(MapField))
		| Service(@base, fs) => AST2.Definition.Service   (d.name, ns, @base, fs.Map(MapFunction))
		| Struct(fields)     => AST2.Definition.Struct    (d.name, ns, fields.Map(MapField))
		| Typedef(ty)        => AST2.Definition.Typedef   (d.name, ns, MapType(ty))
		}
		and MapEnumMember(m)
		{
			AST2.EnumMember(m.Name, m.Value)
		}
		and MapField(f)
		{
			AST2.Field(f.ID, f.Requiredness.Map(MapFieldRequiredness), MapType(f.Type), f.Name)
		}
		and MapFieldRequiredness(fr : AST.FieldRequiredness)
		{
			fr :> AST2.FieldRequiredness
		}
		and MapFunction(f)
		{
			AST2.Function
				( name       = f.Name
				, returnType = MapType(f.ReturnType)
				, fields     = f.Fields.Map(MapField)
				, throws     = f.Throws.Map(MapField)
				, isOneWay   = f.IsOneWay
				);
		}
		and MapType(ty)
		{
			def newTy = AST2.Type(null, ty.Parameters.Map(MapType));
			unresolvedTypes.Add(newTy, ty);
			newTy;
		}

		def definitions = Hashtable();
		foreach (d in document.Definitions)
			definitions.Add(d.name, MapDefinition(d));
		definitions.Add("void", AST2.Definition.Void("void", globalNS));
		foreach (name in [ "bool", "byte", "float", "double", "i16", "i32", "i64" ])
			definitions.Add(name, AST2.Definition.Primitive(name, globalNS));
		foreach (name in [ "binary", "list", /*"map", "set",*/ "string" ])
			definitions.Add(name, AST2.Definition.Collection(name, globalNS));

		//--------------
		// resolve types
		//--------------

		foreach ((newTy, oldTy) in unresolvedTypes)
		{
			if (definitions.Contains(oldTy.Name))
				newTy.Definition = definitions[oldTy.Name];
			else
				throw GeneratorException($"Undefined type '$(oldTy.Name)' in '$srcName'.");
		}

		AST2.Api(definitions);
	}
}
