using System.Collections.Generic;
using System.Text;

namespace AST2
{
	[ Record ]
	public variant Definition
	{
	| Collection
	| Const     { ty : Type; value : Constant }
	| Enum      { members : IEnumerable[EnumMember] }
	| Exception { fields : IEnumerable[Field] }
	| Primitive
	| Service   { @base : option[string]; functions : IEnumerable[Function] }
	| Struct    { fields : IEnumerable[Field] }
	| Typedef   { ty : Type }
	| Void

		public name : string;

		public ns : Namespace;

		public override ToString() : string
		{
			def CompoundToString[T]
				( kind    : string
				, name    : string
				, members : IEnumerable[T]
				)
			{
				def text = StringBuilder();
				_ = text.AppendLine($"$kind $name");
				_ = text.AppendLine("{");
				foreach (member in members)
					_ = text.AppendLine("\t" + member.ToString());
				_ = text.Append("}");
				text.ToString();
			}
			def MakeServiceName(name, @base)
			{
			| (name, Some(@base)) => $"$name extends $base"
			| (name, None)        => name
			}
			match (this)
			{
			| Collection                => name
			| Const(ty, value)          => $"const $ty $name = $value"
			| Enum(members)             => CompoundToString("enum", name, members)
			| Exception(fields)         => CompoundToString("exception", name, fields)
			| Primitive                 => name
			| Service(@base, functions) => CompoundToString("service", MakeServiceName(name, @base), functions)
			| Struct(fields)            => CompoundToString("struct", name, fields)
			| Typedef(ty)               => $"typedef $ty $name"
			| Void                      => name
			}
		}
	}
}
